plugins {
	id "io.freefair.lombok"
	id "io.spring.dependency-management"
	id "java"
	id "org.flywaydb.flyway"
	id "org.springframework.boot"
	id "org.openapi.generator"
}


group = "io.github.bbortt.k6.dashboard"
version = "${version}"


java {
	sourceCompatibility = "21"
}


repositories {
	mavenCentral()
}


dependencies {
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	compileOnly "org.projectlombok:lombok:${lombokVersion}"

	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
	implementation "io.hypersistence:hypersistence-utils-hibernate-63:${hypersistenceVersion}"
	implementation "io.swagger.core.v3:swagger-annotations-jakarta:${swaggerAnnotationsJakartaVersion}"
	implementation "jakarta.validation:jakarta.validation-api"
	implementation "org.flywaydb:flyway-core"
	implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-web"

	developmentOnly "org.springframework.boot:spring-boot-devtools"

	runtimeOnly "org.postgresql:postgresql"

	testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
}


flyway {
	url = "jdbc:postgresql://localhost:5432/k6_dashboard"
	user = "k6_dashboard"
	password = "KrPPCHdYSXz6wMct5tUK"
}


tasks.named("test") {
	useJUnitPlatform()
}


task openApiGenerateBackend(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	generatorName = "spring"
	inputSpec = "$rootDir/openapi/k6-dashboard.yml".toString()
	outputDir = "$buildDir/openapi".toString()
	apiPackage = "io.github.bbortt.k6.dashboard.web.api"
	modelPackage = "io.github.bbortt.k6.dashboard.service.api.dto"
	apiFilesConstrainedTo = [""]
	modelFilesConstrainedTo = [""]
	supportingFilesConstrainedTo = ["ApiUtil.java"]
	configOptions = [
			delegatePattern: "true",
			useOptional    : "true",
			useSpringBoot3 : "true",
			useTags        : "true",
			title          : "k6-dashboard"
	]
	validateSpec = true
	importMappings = [Problem:"org.springframework.http.ProblemDetail"]
}

sourceSets {
	main {
		java {
			srcDir file("${project.buildDir.path}/openapi/src/main/java")
		}
	}
}

compileJava.dependsOn "openApiGenerateBackend"
